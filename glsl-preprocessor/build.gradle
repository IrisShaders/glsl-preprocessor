plugins {
  // Apply the java-library plugin for API and implementation separation.
  id 'java-library'
  id 'maven-publish'
  id 'signing'
  id 'org.danilopianini.publish-on-central' version '0.7.5' // a newer version 2.0.6 exists but it's annoying to use
  id 'jacoco'
  id 'com.adarshr.test-logger' version '3.2.0'
  // id "com.github.spotbugs" version "5.0.13"
}

repositories {
  // Use Maven Central for resolving dependencies.
  mavenCentral()
}

group = "io.github.douira"
version = '2.0.0-pre1'

dependencies {
  implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.17'
  implementation group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.7.3'

  testImplementation group: 'io.github.origin-energy', name:'java-snapshot-testing-junit5', version: '4.+'

  testImplementation 'org.junit.jupiter:junit-jupiter:5.11.4'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
  useJUnitPlatform()
  
  // report is always generated after tests run
  finalizedBy jacocoTestReport
  testLogging.showStandardStreams = true
  outputs.upToDateWhen { false }
}

testlogger {
  theme 'standard-parallel'
  slowThreshold 5000
  showSimpleNames true
  showPassed false
}

jacocoTestReport {
  // tests are required to run before generating the report
  dependsOn test
  reports {
    xml.required = true
    html.required = true
  }
}

publishOnCentral {
  configureMavenCentral.set(true)
  projectDescription.set('GLSL Preprocessing with the C Preprocessor in Java - based on JCPP')
  projectLongName.set('glsl-preprocessor')
  projectUrl.set('https://github.com/IrisShaders/glsl-preprocessor')
  scmConnection.set('git@github.com:IrisShaders/glsl-preprocessor.git')
  licenseName.set('GNU General Public License v3.0 with exceptions')
  licenseUrl.set('https://www.gnu.org/licenses/gpl-3.0.en.html')
}

publishing {
  publications {
    javaMaven(MavenPublication) {
      pom {
        developers {
          developer {
            name.set('douira')
            email.set('douira100@gmail.com')
            url.set('https://douira.github.io')
          }
        }
      }
    }
  }
}
